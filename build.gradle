plugins {
  id 'java'
  id 'eclipse'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '1.2.3'
}

repositories {
  mavenLocal()
  mavenCentral()
  jcenter()
}

version = '2.0-SNAPSHOT'
sourceCompatibility = '1.8'
mainClassName = 'io.vertx.core.Launcher'

def vertxVersion = '3.4.1'
def resteasyVersion = '3.1.1.Final'
def junitVersion = '4.12'
def mainVerticleName = 'social.cut.Server'
def watchForChange = 'src/**/*'
def configFile = 'src/main/resources/config.dev.json'
def doOnChange = 'gradle classes'

dependencies {
  compile "io.vertx:vertx-core:$vertxVersion"
  compile "io.vertx:vertx-codegen:$vertxVersion"
  compile "io.vertx:vertx-web:$vertxVersion"
  compile "io.vertx:vertx-mongo-client:$vertxVersion"
  compile "org.jboss.resteasy:resteasy-vertx:$resteasyVersion"
  compile "org.jboss.resteasy:resteasy-jackson-provider:$resteasyVersion"
  testCompile "junit:junit:$junitVersion"
  testCompile "io.vertx:vertx-unit:$vertxVersion"
}

shadowJar {
  classifier = 'fat'
  manifest {
      attributes "Main-Verticle": mainVerticleName
  }
  mergeServiceFiles {
    include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
  }
}

run {
  args = ['run', mainVerticleName,
    "--redeploy=$watchForChange",
    "--launcher-class=$mainClassName",
    "--on-redeploy=$doOnChange",
    "--conf=$configFile"]
}

task wrapper(type: Wrapper) {
  gradleVersion = '3.4.1'
}
